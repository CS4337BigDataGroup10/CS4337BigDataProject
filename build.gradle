plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.0' apply false
	id 'io.spring.dependency-management' version '1.1.0' apply false
}

allprojects {
	group = 'com.example.Microservices'
	version = '1.0.0-SNAPSHOT'

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java'

	dependencyManagement {
		imports {
			// Spring Cloud BOM for dependency version management
			mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2022.0.4'
		}
	}

	dependencies {
		// Spring Boot dependencies
		implementation 'org.springframework.boot:spring-boot-starter'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.boot:spring-boot-starter-security'


		// Eureka Client for service discovery
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

		// MySQL Database
		implementation 'mysql:mysql-connector-java:8.0.32'

		// JSON Web Token (JWT) Dependencies
		implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
		runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
		runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

		// Testing
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'com.h2database:h2'

		// Byte Buddy for Testing
		testImplementation 'net.bytebuddy:byte-buddy:1.14.4'

		//SwaggerAPI
		implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'
		implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'

	}

	java {
		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_17
	}

	test {
		useJUnitPlatform()
		jvmArgs '-Dnet.bytebuddy.experimental=true'
	}
}
